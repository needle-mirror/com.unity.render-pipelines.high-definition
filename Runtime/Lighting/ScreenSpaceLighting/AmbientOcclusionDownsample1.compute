//
// This is a modified version of the SSAO renderer from Microsoft's MiniEngine
// library. The copyright notice from the original version is included below.
//
// The original source code of MiniEngine is available on GitHub.
// https://github.com/Microsoft/DirectX-Graphics-Samples
//

//
// Copyright (c) Microsoft. All rights reserved.
// This code is licensed under the MIT License (MIT).
// THIS CODE IS PROVIDED *AS IS* WITHOUT WARRANTY OF
// ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING ANY
// IMPLIED WARRANTIES OF FITNESS FOR A PARTICULAR
// PURPOSE, MERCHANTABILITY, OR NON-INFRINGEMENT.
//
// Developed by Minigraph
//
// Author:  James Stanard
//

#pragma warning(disable : 3568)
#pragma only_renderers d3d11 ps4 xboxone vulkan metal switch

#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"

#pragma kernel KMain            MAIN=KMain
#pragma kernel KMain_MSAA       MAIN=KMain_MSAA      MSAA

TEXTURE2D_X(_Depth);

#if MSAA
RW_TEXTURE2D_X(float2, _LinearZ);
RW_TEXTURE2D_X(float2, _DS2x);
RW_TEXTURE2D_ARRAY(float2, _DS2xAtlas);
RW_TEXTURE2D_X(float2, _DS4x);
RW_TEXTURE2D_ARRAY(float2, _DS4xAtlas);

groupshared float2 gs_cacheW[256];
#else
RW_TEXTURE2D_X(float, _LinearZ);
RW_TEXTURE2D_X(float, _DS2x);
RW_TEXTURE2D_ARRAY(float, _DS2xAtlas);
RW_TEXTURE2D_X(float, _DS4x);
RW_TEXTURE2D_ARRAY(float, _DS4xAtlas);

groupshared float gs_cacheW[256];
#endif

#ifdef MSAA
float2 Linearize(uint2 st)
{
    float depthMin = _Depth[COORD_TEXTURE2D_X(st)].y;
    float depthMax = _Depth[COORD_TEXTURE2D_X(st)].x;

    float2 depth = float2(depthMin, depthMax);
    float2 dist = 1.0 / (_ZBufferParams.x * depth + _ZBufferParams.y);
#ifdef UNITY_REVERSED_Z
    if (depth.x == 0) dist.x = 1e5;
    if (depth.y == 0) dist.y = 1e5;
#else
    if (depth.x == 1) dist.x = 1e5;
    if (depth.y == 1) dist.y = 1e5;
#endif
    _LinearZ[COORD_TEXTURE2D_X(st)] = dist;
    return dist;
}
#else
float Linearize(uint2 st)
{
    float depth = _Depth[COORD_TEXTURE2D_X(st)].x;
    float dist = 1.0 / (_ZBufferParams.x * depth + _ZBufferParams.y);
#ifdef UNITY_REVERSED_Z
    if (depth == 0) dist = 1e5;
#else
    if (depth == 1) dist = 1e5;
#endif
    _LinearZ[COORD_TEXTURE2D_X(st)] = dist;
    return dist;
}
#endif

[numthreads(8, 8, 1)]
void MAIN(uint2 Gid : SV_GroupID, uint GI : SV_GroupIndex, uint2 GTid : SV_GroupThreadID, uint3 DTid : SV_DispatchThreadID)
{
    UNITY_STEREO_ASSIGN_COMPUTE_EYE_INDEX(DTid.z);

    uint2 startST = Gid << 4 | GTid;
    uint destIdx = GTid.y << 4 | GTid.x;
    gs_cacheW[destIdx +  0 ] = Linearize(startST | uint2(0, 0));
    gs_cacheW[destIdx +  8 ] = Linearize(startST | uint2(8, 0));
    gs_cacheW[destIdx + 128] = Linearize(startST | uint2(0, 8));
    gs_cacheW[destIdx + 136] = Linearize(startST | uint2(8, 8));

    GroupMemoryBarrierWithGroupSync();

    uint ldsIndex = (GTid.x << 1) | (GTid.y << 5);

#ifdef MSAA
    float2 w1 = gs_cacheW[ldsIndex];
#else
    float w1 = gs_cacheW[ldsIndex];
#endif

    uint2 st = DTid.xy;
    uint slice = ((st.x & 3) | (st.y << 2)) & 15;
#if defined(UNITY_STEREO_INSTANCING_ENABLED)
    slice += DTid.z << 4; // stereo eye indexing (16 slices per eye)
#endif
    _DS2x[COORD_TEXTURE2D_X(st)] = w1;
    _DS2xAtlas[uint3(st >> 2, slice)] = w1;

    if ((GI & 011) == 0)
    {
        st = DTid.xy >> 1;
        slice = ((st.x & 3) | (st.y << 2)) & 15;
#if defined(UNITY_STEREO_INSTANCING_ENABLED)
        slice += DTid.z << 4; // stereo eye indexing (16 slices per eye)
#endif
        _DS4x[COORD_TEXTURE2D_X(st)] = w1;
        _DS4xAtlas[uint3(st >> 2, slice)] = w1;
    }

}
